buildscript {
    ext {
        // Dependency versions
        // Must be defined here instead of "gradle.properties" because of
        // https://github.com/dependabot/dependabot-core/issues/1618
        awsLambdaJavaCore = '1.2.1'
        detektVersion = '1.17.1'
        gitVersioningVersion = '6.1.0'
        junitVersion = '5.7.2'
        kotlinVersion = '1.4.32'
        mockitoVersion = '3.2.0'
        retrofitVersion = '2.9.0'
        sonarqubeVersion = '3.3'
    }
}

plugins {
    id "java"
    id 'jacoco'
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "me.qoomon.git-versioning" version "$gitVersioningVersion"
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
    id "org.sonarqube" version "$sonarqubeVersion"
}

description "Stefans Smart Home Alexa Skill"
group "de.stefan_oltmann.smarthome.alexaskill"
version ""

gitVersioning.apply {
    branch {
        pattern = 'master'
        versionFormat = '${commit.short}'
    }
    tag {
        pattern = 'v(?<tagVersion>[0-9].*)'
        versionFormat = '${tagVersion}'
    }
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {

    dependsOn test // tests are required to run before generating the report

    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

detekt {
    allRules = true
    config = files("$projectDir/detekt.yml")

    // Don't break the build. Just report.
    ignoreFailures = true
}

sonarqube {
    properties {
        property "sonar.projectKey", "smart-home-alexaskill"
        property "sonar.projectName", "Stefans Smart Home Alexa Skill"
        property "sonar.organization", "stefanoltmann"
        property "sonar.host.url", "https://sonarcloud.io"
        // Include Detekt issues
        property "sonar.kotlin.detekt.reportPaths", "build/reports/detekt/detekt.xml"
        // JaCoCo Report
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

// SonarQube should always contain Detekt issues
tasks.findByPath(':sonarqube').dependsOn('detekt')

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    // The module-info is duplicated and we don't need it.
    exclude 'META-INF/versions/9/module-info.class'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter() // needed by Detekt
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // AWS Lambda Framework
    implementation "com.amazonaws:aws-lambda-java-core:$awsLambdaJavaCore"

    // REST API Calls
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Unit Tests
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoVersion"
}
